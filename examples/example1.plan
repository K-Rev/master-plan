/*
* This project is a business plan, which vision is state in
* the "root" project - which is run a successful tech-based
* business in a certain market.
*/

root {
  title "business"
  description "can we run a successful business"
  owner "CEO"
} x + p;

x {
  title "build technology"
  description "can we build the technology ourselves"
} h * b;

h = hire + acquihire;

hire {
  title "hire"
  description "can we attract and retain talent"
  cost 20
  owner "HR"
  trust 50%
};

acquihire {
  description "can we buy talent"
  cost 100
  owner "HR"
  trust 100%
};

b {
  title "build"
  description "our technology can be built and scale"
} phase1 -> phase2 -> phase3;

phase1 {
  title "validate prototype"
  trust 70%
  progress 100%
  owner "engineering"
};

phase2 {
  title "launch in small market"
  trust 50%
  progress 32%
  owner "engineering"
};

phase3 {
  title "scale nationwide"
  trust 20%
  owner "engineering"
};

// Another way to get technology is to parner,
// instead of building it:

p {
  title "tech partner"
  description "secure a tech partnership"
} (approvalProcess -> sa) + (approvalProcess -> sb);

sa {
  title "supplier A"
  trust 90%
  cost 10
  url "www.supplier.a.com"
  owner "partnerships"
};

sb {
  title "supplier B"
  trust 60%
  cost 5
  url "www.supplier.b.com"
  owner "partnerships"
};

// Approval process is a sub-project we have to go once
// regardless of which partnership, will "open the way"

approvalProcess "approval process" legal -> budget -> executive;

legal {
  description "figure out how to write the contract"
};

budget {
  description "can we afford?"
};
